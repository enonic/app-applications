plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.5'
    id 'com.enonic.xp.app' version '3.6.1'
    id "com.github.node-gradle.node" version '7.0.2'
}

def xpVersion = version

app {
    name = 'com.enonic.xp.app.applications'
    group = 'com.enonic.xp'
    displayName = 'Applications'
    systemApp = true
    systemVersion = "${version}"
    devSourcePaths += file( "$rootDir/../lib-admin-ui/src/main/resources" )
}

configurations {
    devResources {
        attributes {
            attribute( LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named( LibraryElements, 'dev-resources' ) )
        }
    }
    all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenLocal()
    xp.enonicRepo('dev')
    mavenCentral()
}

apply from: "$projectDir/gradle/env.gradle"
apply from: "$projectDir/gradle/node.gradle"
apply from: "$projectDir/gradle/lib-admin.gradle"

dependencies {
    implementation "com.enonic.xp:jaxrs-api:${xpVersion}"
    implementation "com.enonic.xp:admin-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
    devResources "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
    include "com.enonic.lib:lib-mustache:2.1.1"
    include "com.enonic.lib:lib-asset:1.0.3"

    testImplementation(platform("org.junit:junit-bom:5.12.2"))
    testImplementation(platform("org.mockito:mockito-bom:5.17.0"))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation( testFixtures( "com.enonic.xp:jaxrs-impl:${xpVersion}" ) )
    testImplementation( testFixtures( "com.enonic.xp:core-app:${xpVersion}" ) )
}

tasks.register('lint', NpmTask ) {
    group "verification"
    dependsOn( 'npmInstall' )
    args = ['run', 'lint']
    outputs.upToDateWhen { false }
}

check.configure {
    dependsOn( 'lint' )
}

tasks.register('webpack', NpmTask ) {
    dependsOn( 'npmInstall' )
    environment = ['NODE_ENV': nodeEnvironment()]
    description = 'Build UI resources (ts, css, etc).'
    args = ['run', 'build']
    inputs.dir '.xp/dev'
    inputs.dir 'src/main/resources/assets'
    inputs.file 'webpack.config.js'
}

jar {
    dependsOn += webpack
}

processResources {
    exclude 'assets/**'  // Excluded because webpack does it
}

test {
    useJUnitPlatform()
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-XX:TieredStopAtLevel=1'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
    mustRunAfter jar
}

check.dependsOn jacocoTestReport
