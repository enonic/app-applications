plugins {
    id 'java'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.2'
    id 'com.enonic.xp.app' version '3.4.0'
    id "com.github.node-gradle.node" version '7.0.0'
}

def xpVersion = version

app {
    name = 'com.enonic.xp.app.applications'
    group = 'com.enonic.xp'
    displayName = 'Applications'
    systemApp = true
    systemVersion = "${version}"
    devSourcePaths += file( "$rootDir/../lib-admin-ui/src/main/resources" )
}

configurations {
    devResources {
        attributes {
            attribute( LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named( LibraryElements, 'dev-resources' ) )
        }
    }
}

apply from: "$projectDir/gradle/env.gradle"
apply from: "$projectDir/gradle/node.gradle"
apply from: "$projectDir/gradle/lib-admin.gradle"

dependencies {
    implementation "com.enonic.xp:jaxrs-api:${xpVersion}"
    implementation "com.enonic.xp:admin-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"

    include "com.enonic.xp:lib-admin:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"

    include "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"
    devResources "com.enonic.lib:lib-admin-ui:${libAdminUiVersion}"

    include "com.enonic.lib:lib-mustache:2.1.0"
    include 'com.enonic.lib:lib-router:3.1.0'
    include 'com.enonic.lib:lib-static:1.0.3'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.9.1"
    testImplementation "org.mockito:mockito-core:4.8.0"
    testImplementation( testFixtures( "com.enonic.xp:jaxrs-impl:${xpVersion}" ) )
    testImplementation( testFixtures( "com.enonic.xp:core-app:${xpVersion}" ) )

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.9.1"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenLocal()
    xp.enonicRepo('dev')
    mavenCentral()
}

tasks.withType(Copy) {
    includeEmptyDirs = false
}

tasks.register('lint', NpmTask ) {
    group "verification"
    dependsOn( 'npmInstall' )
    args = ['run', 'lint']
    outputs.upToDateWhen { false }
}

check.configure {
    dependsOn( 'lint', 'npmCheck' )
}

tasks.register( 'npmBuild', NpmTask) {
    dependsOn npmInstall
    description = 'Build UI resources (ts, css, etc).'
    environment = [
        'FORCE_COLOR': 'true',
        'LOG_LEVEL_FROM_GRADLE': gradle.startParameter.logLevel.toString(),
        'NODE_ENV': nodeEnvironment()
    ]
    args = ['run', 'build']
    inputs.dir '.xp/dev'
    inputs.dir 'src/main/resources'
    inputs.file 'tsup.config.ts'
    outputs.dir "$buildDir/build/resources/main"
}

tasks.register('npmBuildTests', NpmTask) {
    dependsOn( 'npmBuild' )
    environment = [
        'FORCE_COLOR': 'true',
        'LOG_LEVEL_FROM_GRADLE': gradle.startParameter.logLevel.toString(),
        'NODE_ENV': nodeEnvironment()
    ]
    description = 'Build tests'
    args = ['run', '--silent', 'build:tests']
    inputs.dir 'src/test/resources'
    outputs.dir "$buildDir/build/resources/test"
    inputs.file 'tsup.config.ts'
}

test.dependsOn npmBuildTests

processResources {
    exclude '**/tsconfig.json'
    exclude '**/*.ts'
    exclude 'assets/**/*.less'
    exclude 'assets/**/*.woff'
    exclude 'assets/**/*.woff2'

    if ( isProd() )
    {
        exclude 'assets/**/*.map'
    }
}

jar {
    outputs.dir "$buildDir/resources/main"

    dependsOn += test
    dependsOn += npmBuild
}

test {
    useJUnitPlatform()
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-noverify', '-XX:TieredStopAtLevel=1'
}
